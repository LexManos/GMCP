import org.gradle.api.artifacts.maven.MavenDeployment

apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'groovy'
apply plugin: 'signing'
apply plugin: 'maven'

group = 'com.github.abrarsyed.gmcp'
version = '0.8.5-SNAPSHOT'
archivesBaseName = 'GMCP'
targetCompatibility = '1.6'
sourceCompatibility = '1.6'

repositories {  mavenCentral() }

jar
{
    manifest {
        attributes 'version':project.version, 'javaCompliance': project.targetCompatibility
        attributes 'group':project.group
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

//task groovydocJar(type: Jar, dependsOn: groovydoc) {
//    classifier = 'groovydoc'
//    from 'build/docs/groovydoc'
//}

configurations
{
    packed
    compile.extendsFrom packed
}

dependencies
{
    compile gradleApi()
    compile localGroovy()

    // all of these marked as packed.
    //packed fileTree(dir: 'lib', include: '*.jar')

    compile 'net.sourceforge.argo:argo:3.+'
    compile 'net.sf.opencsv:opencsv:2.3'
    compile 'com.github.tony19:named-regexp:0.1.9'
    compile 'net.md-5:SpecialSource:1.+'
    compile 'com.cloudbees:diff4j:1.1' // for difing and patching
    compile 'com.github.abrarsyed.jastyle:jAstyle:1.1' // formatting

    // lombok
    compile 'org.projectlombok:lombok:0.11.+'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
//    archives groovydocJar
    archives sourcesJar
}

signing { sign configurations.archives }

uploadArchives {
    repositories.mavenDeployer {

        beforeDeployment { MavenDeployment deployment ->
            signing.signPom(deployment)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

//        local testing only.
        //snapshotRepository(url: 'file://'+project.projectDir.getPath()+"/../MavenRepo")

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom {
            groupId = project.group
            version = project.version
            artifactId = project.archivesBaseName
            project {
                name project.archivesBaseName
                packaging 'jar'
                description 'Groovy Minecraft Coder Pack'
                url 'https://github.com/AbrarSyed/GMCP'

                scm {
                    url 'https://github.com/AbrarSyed/GMCP'
                    connection 'scm:git:git://github.com/AbrarSyed/GMCP.git'
                    developerConnection 'scm:git:git@github.com:AbrarSyed/GMCP.git'
                }

                issueManagement {
                    system 'github'
                    url 'https://github.com/AbrarSyed/GMCP/issues'
                }

                licenses {
                    license {
                        name 'GNU Lesser General Public License (LGPL), Version 3'
                        url 'http://www.fsf.org/licensing/licenses/lgpl.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'AbrarSyed'
                        name 'Abrar Syed'
                        roles {
                            role 'packager'
                            role 'developer'
                        }
                    }
                }
            }
        }
        
        // end pom.
        
    }
}

// wrapper
task wrapper(type: Wrapper) { gradleVersion = '1.8' }